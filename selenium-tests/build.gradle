import com.enonic.uitest.server.ServerInstance

apply plugin: 'groovy'

configurations {
    distro
}

ext {
    xpVersion = '5.0.0-SNAPSHOT'
    unpackDir = "$buildDir/install"
    distroDir = "$unpackDir/enonic-xp-$xpVersion"
}

dependencies {
    compile 'org.seleniumhq.selenium:selenium-java:2.44.0'
    compile 'com.enonic.xp:core-api:5.0.0-SNAPSHOT'
    compile 'log4j:log4j:1.2.17'
    compile 'org.codehaus.groovy:groovy-all:2.1.8'
    compile 'org.spockframework:spock-core:0.7-groovy-2.0'
    compile 'org.gebish:geb-spock:0.10.0'

    distro "com.enonic.xp:distro:$xpVersion@zip"
}

task buildModules(
    dependsOn: [':test-modules:first-module:install', ':test-modules:simple-page:install', ':test-modules:all-contenttypes:install'] )

task unpackDistro( type: Copy ) {
    from {
        configurations.distro.collect { zipTree( it ) }
    }
    into file( unpackDir )
}

task startServer( dependsOn: unpackDistro ) {
    doLast {
        // logging.captureStandardOutput LogLevel.INFO
        ext.xpServer = new ServerInstance()
        ext.xpServer.installDir = file( distroDir )
        ext.xpServer.startupDelay = 20000
        ext.xpServer.start()
    }
}

task stopServer << {
    ext.xpServer.stop()
}

//task clearData( type: Test, dependsOn: ['installModules'] ) {
task initializeData( type: Test, dependsOn: ['startServer'] ) {
    scanForTestClasses = true
    exclude '**/*Spec.class'
    exclude '**/*Suite.*'
    include '**/CleanAndInitializeData.class'
}

task installSimpleSiteModule( type: Test, dependsOn: ['buildModules', 'startServer'] ) {

    scanForTestClasses = true
    exclude '**/*Spec.class'
    include '**/liveedit/InstallSimpleSiteModule.class'
}

tasks.withType( Test ) {
    jvmArgs '-Xms128m', '-Xmx1024m'
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        displayGranularity = 0
    }
}

task testContentManager( type: Test, dependsOn: [startServer, initializeData] ) {
    scanForTestClasses = false
    include '**/*Spec.class'
    exclude '**/CleanAndInitializeData.class'
    exclude '**/content/ContentUploadSpec.class'
    exclude '**/content/liveedit/**Spec.class'
    forkEvery = null
}

task testUser( type: Test, dependsOn: [startServer, initializeData] ) {
    scanForTestClasses = false
    include '**/user/*Spec.class'
    exclude '**/CleanAndInitializeData.class'
    exclude '**/content/**'
    exclude '**/module/**'
    forkEvery = null
}

task testModule( type: Test, dependsOn: [startServer, initializeData] ) {
    scanForTestClasses = false
    include '**/module/*Spec.class'
    exclude '**/CleanAndInitializeData.class'
    exclude '**/content/**'
    exclude '**/user/**'
    forkEvery = null
}

task testSimpleSiteLiveEdit( type: Test, dependsOn: ['installSimpleSiteModule', 'initializeData'] ) {
    scanForTestClasses = false

    include '**/liveedit/CreateSiteWithLayoutSpec.class'
    exclude '**/CleanAndInitializeData.class'

    exclude '**/module/**'
    exclude '**/user/**'
    forkEvery = null
    initializeData.shouldRunAfter installSimpleSiteModule
}


task testUploadContent( type: Test, dependsOn: ['initializeData'] ) {
    scanForTestClasses = false
    exclude '**/CleanAndInitializeData.class'
    exclude '**/module/**'
    exclude '**/user/**'
    include '**/content/ContentUploadSpec.class'
    forkEvery = null
}
testModule.finalizedBy = ['stopServer']
testSimpleSiteLiveEdit.finalizedBy = ['stopServer']
testUser.finalizedBy = ['stopServer']
testContentManager.finalizedBy = ['stopServer']

