import com.enonic.uitest.server.ServerInstance

apply plugin: 'groovy'


configurations {
    distro
}

ext {
    xpVersion = '6.0.0-SNAPSHOT'
    unpackDir = "$buildDir${File.separator}install"
    screenshotsDir = "$buildDir${File.separator}screenshots"
    reportsDir = "$buildDir${File.separator}repots"
    distroDir = "$unpackDir${File.separator}enonic-xp-$xpVersion"
    deployDir = "$distroDir${File.separator}home${File.separator}deploy"
    xpHome = "$distroDir${File.separator}home"
}

dependencies {
    compile 'org.seleniumhq.selenium:selenium-java:2.46.0'
    compile 'com.enonic.xp:core-api:6.0.0-SNAPSHOT'
    compile 'log4j:log4j:1.2.17'
    compile 'org.codehaus.groovy:groovy-all:2.1.8'
    compile 'org.spockframework:spock-core:0.7-groovy-2.0'
    compile 'org.gebish:geb-spock:0.10.0'

    distro "com.enonic.xp:distro:$xpVersion@zip"
}
task copyContentTypes( type: Copy ) {
    from '../test-modules/all-contenttypes/build/libs/'
    include '**/*.jar'
    into file( deployDir )
}

task copyTestModules( type: Copy ) {
    from '../test-modules/first-module/build/libs/'
    from '../test-modules/second-module/build/libs/'
    from '../test-modules/third-module/build/libs/'
    from '../test-modules/fourth-module/build/libs/'
    from '../test-modules/test-module/build/libs/'
    include '**/*.jar'
    into file( deployDir )
}
task deleteFolders( type: Delete ) {
    delete unpackDir, screenshotsDir, reportsDir
}
task buildModules(
    dependsOn: [':test-modules:first-module:install', ':test-modules:simple-page:install', ':test-modules:all-contenttypes:install',
                ':test-modules:second-module:install', ':test-modules:third-module:install', ':test-modules:fourth-module:install', ':test-modules:test-module:install'] )

task unpackDistro( type: Copy ) {
    from {
        configurations.distro.collect { zipTree( it ) }
    }
    into file( unpackDir )
}

ext {
    xpServer = new ServerInstance()
}

task startServer( dependsOn: unpackDistro ) {
    doLast {
        // logging.captureStandardOutput LogLevel.INFO
        // ext.xpServer = new ServerInstance()
        xpServer.installDir = file( distroDir )
        xpServer.startupDelay = 20000
        xpServer.start()
    }
}

task stopServer << {
    xpServer.stop()
}


task installSimpleSiteModule( type: Test, dependsOn: ['buildModules', 'startServer'] ) {
    scanForTestClasses = true
    exclude '**/*Spec.class'
    include '**/liveedit/InstallSimpleSiteModule.class'
}

tasks.withType( Test ) {
    jvmArgs '-Xms128m', '-Xmx1024m'
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        displayGranularity = 0
    }
}

task testContentManager( type: Test, dependsOn: [deleteFolders, startServer, copyContentTypes] ) {
    scanForTestClasses = false
    include '**/*Spec.class'
    exclude '**/module/**'
    exclude '**/user/**'
    exclude '**/content/ContentUploadSpec.class'
    exclude '**/content/liveedit/**Spec.class'
    exclude '**/content/input_types/*Spec.class'
    exclude '**/content/move_publish_sort/*Spec.class'
    forkEvery = null
    startServer.mustRunAfter deleteFolders
    copyContentTypes.mustRunAfter startServer
}
task testInputTypes( type: Test, dependsOn: [buildModules, deleteFolders, startServer, copyContentTypes] ) {
    scanForTestClasses = false
    include '**/content/input_types/*Spec.class'
    exclude '**/module/**'
    exclude '**/user/**'
    exclude '**/content/ContentUploadSpec.class'
    exclude '**/content/liveedit/**Spec.class'
    forkEvery = null
    deleteFolders.mustRunAfter buildModules
    startServer.mustRunAfter deleteFolders
    copyContentTypes.mustRunAfter startServer
}

task testPublishMoveSort( type: Test, dependsOn: [buildModules, deleteFolders, startServer, copyContentTypes] ) {
    scanForTestClasses = false
    include '**/content/move_publish_sort/*Spec.class'
    exclude '**/module/**'
    exclude '**/user/**'
    forkEvery = null
    copyContentTypes.shouldRunAfter startServer
}

task testUser( type: Test, dependsOn: [deleteFolders, startServer] ) {
    scanForTestClasses = false
    include '**/user/*Spec.class'
    exclude '**/content/**'
    exclude '**/module/**'
    forkEvery = null
    startServer.shouldRunAfter deleteFolders
}

task testModule( type: Test, dependsOn: [buildModules, deleteFolders, startServer, copyTestModules] ) {
    println xpHome
    println "XP_HOME is:"
    println System.getenv( "XP_HOME" );


    scanForTestClasses = false
    include '**/module/*Spec.class'
    exclude '**/content/**'
    exclude '**/user/**'
    forkEvery = null
    deleteFolders.mustRunAfter buildModules
    startServer.mustRunAfter deleteFolders
    copyTestModules.mustRunAfter startServer
}

task testSimpleSiteLiveEdit( type: Test, dependsOn: [deleteFolders, installSimpleSiteModule] ) {
    scanForTestClasses = false

    include '**/liveedit/CreateSiteWithLayoutSpec.class'
    include '**/content/ContentUploadSpec.class'
    exclude '**/module/**'
    exclude '**/user/**'
    forkEvery = null
}


task testUploadContent( type: Test, dependsOn: [deleteFolders, buildModules, startServer, copyContentTypes] ) {
    scanForTestClasses = false
    exclude '**/module/**'
    exclude '**/user/**'
    include '**/content/ContentUploadSpec.class'
    forkEvery = null
    copyContentTypes.shouldRunAfter startServer
}

testModule.finalizedBy = ['stopServer']
testSimpleSiteLiveEdit.finalizedBy = ['stopServer']
testUser.finalizedBy = ['stopServer']
testContentManager.finalizedBy = ['stopServer']
testInputTypes.finalizedBy = ['stopServer']

